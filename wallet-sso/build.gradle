apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../and_res_guard.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion = versions.buildTools

    defaultConfig {
        applicationId names.ssoApplicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.gitCommitCount
        versionName names.ssoName
        archivesBaseName = "ViolasSSO-${versionName}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'armeabi'
        }
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            File configFile = project.rootProject.file('sign.properties')
            if (!configFile.exists()) {
                configFile = project.rootProject.file('testSign.properties')
            }
            properties.load(configFile.newDataInputStream())

            storeFile file(properties.getProperty('keyFile'))

            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword');
            storePassword properties.getProperty('storePassword');
        }
        debug {
            storeFile file("../keystore/debug.keystore")
        }
    }

    bundle {
        // 是否分割多语言包
        language {
            enableSplit = false
        }
        // 是否分割资源
        density {
            enableSplit = true
        }
        // 是否分割二进制 so 包资源
        abi {
            enableSplit = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            pseudoLocalesEnabled true
            proguardFiles '../proguard-android-optimize-wallet.txt', 'proguard-rules.pro'
            multiDexKeepProguard file('../multidexKeep.pro')
            signingConfig signingConfigs.release
            buildConfigField "boolean", "SHUFFLE_MNEMONIC", "true"
        }
        debug {
            ext.alwaysUpdateBuildId = false
            multiDexKeepProguard file('../multidexKeep.pro')
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "SHUFFLE_MNEMONIC", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    flavorDimensions "typeNetwork","local"

    productFlavors {
        demo{
            dimension "local"
        }

        googlePlay{
            dimension "local"
        }

        tNet {
            dimension "typeNetwork"
            buildConfigField "String", "TESTNET", '"testnet"'
        }

        mainNet {
            dimension "typeNetwork"
            buildConfigField "String", "TESTNET", '"main"'
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        println(names)
        if (names.contains("mainNet") && names.contains("demo")) {
            setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':lib-common')
    implementation project(':lib-core')
    implementation project(':lib-zbar-scan')
    implementation project(':lib-switch-button')
    implementation project(':lib-libra-core')
    implementation project(':lib-violas-core')
    implementation project(':lib-btc-core')
    implementation project(':module-violas')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation "junit:junit:$versions.test_junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidTest_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.androidTest_espresso"

    kapt "androidx.room:room-compiler:$versions.androidx_room"
    implementation "androidx.room:room-runtime:$versions.androidx_room"
    implementation "androidx.room:room-ktx:$versions.androidx_room"

    // see https://github.com/googlesamples/easypermissions
    implementation 'pub.devrel:easypermissions:3.0.0'

    // EventBus see https://github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //头条适配方案 see https://github.com/JessYanCoding/AndroidAutoSize
    implementation "me.jessyan:autosize:1.1.2"

    // see https://github.com/ittianyu/BottomNavigationViewEx
    implementation 'com.github.ittianyu:BottomNavigationViewEx:2.0.4'

    debugImplementation 'com.idescout.sql:sqlscout-server:4.1'
    releaseImplementation 'com.idescout.sql:sqlscout-server-noop:4.1'

    // see https://github.com/songlongGithub/uTakePhoto
    implementation 'com.sl.utakephoto:uTakePhoto:1.0.6'

    //汉字转拼音
    implementation 'com.github.SilenceDut:jpinyin:v1.0'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // see https://github.com/google/libphonenumber
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.11.0'

    implementation 'top.zibin:Luban:1.1.8'
}
