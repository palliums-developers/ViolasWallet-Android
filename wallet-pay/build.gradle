apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

apply from: '../and_res_guard.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion = versions.buildTools

    ndkVersion "21.1.6352462"

    defaultConfig {
        applicationId names.payApplicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.gitCommitCount
        versionName names.payVersionName
        archivesBaseName = "ViolasPay-${versionName}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            File configFile = project.rootProject.file('payDevelopSign.properties')
            if (!configFile.exists()) {
                configFile = project.rootProject.file('testSign.properties')
            }
            properties.load(configFile.newDataInputStream())

            storeFile file(properties.getProperty('keyFile'))

            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
            storePassword properties.getProperty('storePassword')
        }
        debug {
            storeFile file("../keystore/debug.keystore")
        }
    }

    bundle {
        // 是否分割多语言包
        language {
            enableSplit = false
        }
        // 是否分割资源
        density {
            enableSplit = true
        }
        // 是否分割二进制 so 包资源
        abi {
            enableSplit = true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            pseudoLocalesEnabled true
            proguardFiles '../proguard-android-optimize-wallet.txt', 'proguard-rules.pro'
            multiDexKeepProguard file('../multidexKeep.pro')
            signingConfig signingConfigs.release
            buildConfigField "boolean", "SHUFFLE_MNEMONIC", "true"

            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'armeabi'
            }
        }
        debug {
//            minifyEnabled true
//            zipAlignEnabled true
//            pseudoLocalesEnabled true
//            proguardFiles '../proguard-android-optimize-wallet.txt', 'proguard-rules.pro'
            ext.alwaysUpdateBuildId = false
            multiDexKeepProguard file('../multidexKeep.pro')
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "SHUFFLE_MNEMONIC", "false"

            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'armeabi', "x86", "x86-64"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    flavorDimensions "typeNetwork", "local"

    productFlavors {
        demo {
            dimension "local"
            applicationIdSuffix ".demo"
            resValue "string", "app_name", "ViolasPay Demo"
        }

        googlePlay {
            dimension "local"
            applicationIdSuffix ".beta"
            resValue "string", "app_name", "ViolasPay"
        }

        tNet {
            dimension "typeNetwork"
            buildConfigField "String", "TESTNET", '"testnet"'
        }

        mainNet {
            dimension "typeNetwork"
            buildConfigField "String", "TESTNET", '"main"'
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        println(names)
        if (names.contains("mainNet") && names.contains("demo")) {
            setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':lib-common')
    implementation project(':lib-core')
    implementation project(':lib-zbar-scan')
    implementation project(':lib-switch-button')
    implementation ('org.palliums.lib:libra-core:0.0.4')
    implementation project(':module-violas')
//    implementation project(':lib-violas-core')
    implementation project(':lib-btc-core')
    implementation project(':lib-wallet-connect')
    implementation project(':lib-biometric')

    testImplementation libraries.test_junit
    androidTestImplementation libraries.androidTest_junit
    androidTestImplementation libraries.androidTest_espresso
    debugImplementation libraries.leakcanary_android

    kapt libraries.androidx_room_compiler
    implementation libraries.androidx_room_ktx
    implementation libraries.androidx_room_runtime

    implementation libraries.easypermissions
    implementation libraries.autosize
    implementation libraries.BottomNavigationViewEx

    implementation libraries.eventbus
    implementation(libraries.socketio_client) {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    implementation libraries.glide
    implementation libraries.glide_okhttp3_integration
    kapt libraries.glide_compiler

    //implementation libraries.firebase_core
    implementation libraries.firebase_messaging
    //implementation platform('com.google.firebase:firebase-bom:25.12.0')

    implementation libraries.jpinyin
}
